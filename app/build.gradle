apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdkVersion 30
    ndkVersion '21.3.6528147'
    defaultConfig {
        applicationId "me.magnum.melonds"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 17
        versionName "Beta 1.5.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17 -Wno-write-strings"
            }
        }
        vectorDrawables.useSupportLibrary = true
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xopt-in=kotlin.ExperimentalUnsignedTypes"
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".dev"
        }
    }

    flavorDimensions "version"
    productFlavors {
        playStore {
            dimension "version"
            versionNameSuffix " PS"
        }
        gitHub {
            dimension "version"
            versionNameSuffix " GH"
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == "com.android.support") {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion "26.+"
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // AndroidX
    implementation 'androidx.activity:activity-ktx:1.2.3'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.fragment:fragment-ktx:1.3.5'
    implementation "androidx.hilt:hilt-work:$hiltx_version"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.work:work-rxjava2:$work_version"
    implementation 'com.google.android.material:material:1.4.0'

    // Third-party
    implementation 'com.github.svenoaks:MasterSwitchPreference:0.9.1'
    implementation 'com.google.android:flexbox:2.0.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "io.noties.markwon:core:$markwon_version"
    implementation "io.noties.markwon:image-picasso:$markwon_version"
    implementation "io.noties.markwon:linkify:$markwon_version"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'
    implementation 'org.apache.commons:commons-compress:1.21'
    implementation 'org.tukaani:xz:1.9'

    // KAPT
    kapt "androidx.hilt:hilt-compiler:$hiltx_version"
    kapt "androidx.room:room-compiler:$room_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
}

repositories {
    mavenCentral()
}
